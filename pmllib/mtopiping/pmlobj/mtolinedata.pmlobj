------------------------------------------------------------------------
--
-- (c) Copyright  2017 to Current Year  AVEVA GmbH
--
-- File:            mtolinedata.pmlobj
--   Type:          Object Definition
--   Group:         General Application
--     Keyword:     GEN
--   Module:        design
--   Replaces:
--
-- Author:          Herbert Zöchling
-- Created:         13.02.2017
--
-- Last modified by $Author: herbert.zoechling $ on $Date: 2017/02/13 10:52:52 $
--                  $Revision: 1.00 $
--
-- Description:
--   MTO controller
--
-- *** OLD STYLE PML ***
-- *** COMMAND LINE SYNTAX ***
--
------------------------------------------------------------------------
--

define object mtolinedata
  
  -- settings
  member .qty           is REAL
  member .spref         is DBREF
  member .uid           is STRING
  member .items         is ARRAY

  member .isbolt        is BOOLEAN
  member .boltdata      is BOLTDATA
  
endobject

-- End of object definition for mtolinedata

------------------------------------------------------------------------
--
-- Method:      mtolinedata
--
-- Description: Constructor, using passed
--
-- Method Type: Function/Procedure
-- Arguments:
--   [#] [R/RW] [Data Type] [Description]
--    -
-- Return:
--   [Data Type] [Description]
--
------------------------------------------------------------------------

define method .mtolinedata()
  
  !this.qty = 0
  !this.spref = =0/0
  !this.uid = ||
  !this.items.clear()
  !this.isbolt = FALSE
  !this.boltdata = object BOLTDATA()
  
endmethod

------------------------------------------------------------------------
--
-- Method:      add
--
-- Description: add data to mtoline
--
-- Method Type: Function/Procedure
-- Arguments:
--   [#] [R/RW] [Data Type] [Description]
--    -
-- Return:
--   [Data Type] [Description]
--
------------------------------------------------------------------------

define method .add(!uid is STRING, !item is DBREF, !bolt is BOLTDATA)
  
  if !this.qty neq 0 and !this.isbolt eq FALSE then
    RETURN ERROR 1 'Fatal error in MTO sum - trying to add BOLT to a BRAN MEMBER MTO line!'
  endif
  
  if !this.qty eq 0 then
    !this.isbolt = TRUE
    !this.boltdata = !bolt
    !this.spref = !bolt.spref
    !this.uid = !uid
  endif
  
  !this.qty = !this.qty + !bolt.numboff
  !this.items.append(!item)
  
endmethod

------------------------------------------------------------------------
--
-- Method:      add
--
-- Description: add data to mtoline
--
-- Method Type: Function/Procedure
-- Arguments:
--   [#] [R/RW] [Data Type] [Description]
--    -
-- Return:
--   [Data Type] [Description]
--
------------------------------------------------------------------------

define method .add(!qty is REAL, !uid is STRING, !item is DBREF, !spref is DBREF)
  
  if !this.qty neq 0 and !this.isbolt eq TRUE then
    RETURN ERROR 1 'Fatal error in MTO sum - trying to add BRAN MEMBER to a BOLT MTO line!'
  endif
  
  if !this.qty eq 0 then
    !this.isbolt = FALSE
    !this.spref = !spref
    !this.uid = !uid
  endif
  
  !this.qty = !this.qty + !qty
  !this.items.append(!item)
  
endmethod